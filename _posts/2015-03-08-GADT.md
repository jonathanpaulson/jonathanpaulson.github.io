Consider the following Ocaml module:

    module Expr : sig
      type 'a t =
        | I : int -> int t
        | B : bool -> bool t
        | Add : int t * int t -> int t
        | Eq : 'a t * 'a t -> bool t

      val eval : 'a t -> 'a
    end = struct
      type 'a t =
      | I : int -> int t
      | B : bool -> bool t
      | Add : int t * int t -> int t
      | Eq : 'a t * 'a t -> bool t

      let rec eval : type a . a t -> a =
        fun e -> match e with
        | I n -> n
        | B x -> x
        | Add (e1, e2) -> (eval e1) + (eval e2)
        | Eq (e1, e2) -> eval e1 = eval e2
    end
    (* Usage: *)
    open Expr
    let e1 = Eq (B true, Eq (I 1, I 1))
    let v1 = eval e1

The interesting thing here is that we're writing down the type of the constructors explicitly. Instead of `Add of int t * int t`, we have `Add : int t * int t -> int t`. And this is key: it makes `Add (I 1, I 2)` an `int t` instead of an `'a t` (we don't mention `'a` at all in the arguments to add, so the typechecker has nothing to go on).

_Because_ we know `Add` produces an `int t`, we know `eval (Add _)` is supposed to produce an `int`, so we can actually write the `add` case of eval. If we didn't know that, `eval (Add _)` would have to produce an `'a` out of two `int t`s, which is impossible.

Also of note: the type of `eval` in the implementation. I don't fully understand this, but basically the issue is that `'a` is getting instantiated at different types in the recursive calls (when evaluating the `Eq` case, `'a = bool`, but in the recursive cases, we might have `'a = int`). `a t -> a` is a good enough type for any particular value of `'a`, but to support two different values of `'a` "at the same time", we need to write `type a . a t -> a` (i.e. `forall 'a : 'a t -> 'a`).

This trick of writing down the return types of your constructors explicitly is called `GADT`s ("generalized algebraic data types").

Here's a fancier example:

    module SafeList : sig
      type z = Z
      type 'n s = S
      type ('a, 'size) t

      val nil : ('a, z) t
      val cons : 'a -> ('a, 'n) t -> ('a, 'n s) t

      val head : ('a, _ s) t -> 'a
      val map : ('a, 'n) t -> ('a -> 'b) -> ('b, 'n) t
      val to_pair : ('a, z s s) t -> 'a * 'a
    end = struct
      type z = Z
      type 'n s = S
      type ('a, 'size) t =
        | Nil : ('a, z) t
        | Cons : 'a * ('a, 'size) t -> ('a, 'size s) t

      let head xs = match xs with
        | Cons (x, _) -> x

      let nil = Nil
      let cons x xs = Cons (x, xs)
      let rec map : type n . ('a, n) t -> ('a -> 'b) -> ('b, n) t =
        fun xs f -> match xs with
        | Nil -> Nil
        | Cons (x, xs') -> Cons (f x, map xs' f)
      let rec length : type n . ('a, n) t -> int =
        fun xs -> match xs with
        | Nil -> 0
        | Cons (_, xs') -> 1 + length xs'
      let to_pair xs = match xs with
        | Cons (x1, Cons (x2, Nil)) -> (x1, x2)
    end
    (* Usage: *)
    open SafeList
    let xs = cons 1 (cons 2 nil)
    let ys = map xs Int.to_string
    let y = head ys
    let x1, x2 = to_pair xs

An `('a, 'n) SafeList.t` is a list which holds values of type `'a` and has length `'n`.

The way `'n` works is that it is either `z` ("zero"), or `'n s` (the successor of a number).

This allows us to declare `head` as "a function which takes a non-empty list" (i.e. a list whose length is the successor of some number). `head Nil` is a _compile-time_ error.

This also allows us to declare `to_pair` as "a function which takes a list of length 2". The pattern matching in the implementation of `to_pair` is exhaustive; the only possible form a `('a, z s s) t` can have is `Cons (_, Cons (_, Nil))`, _and_ the compiler knows that.

Note that we need the "forall" trick again to implement `map`, because the type variable `'n` is one less in the recursive call than it is initially.

Exercises:
1. Add `Pair` to `Expr`
2. Add `length` to `SafeList`
3. (HARD) Add `last` to `SafeList` (`last` returns the last element of a non-empty list)
